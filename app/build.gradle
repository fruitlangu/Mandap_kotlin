apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply from: 'gradleScripts/dependencies.gradle'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def ApiUserName = hasProperty('ApiUserName') ? ApiUserName : 'APIUSERNAME'
def ApiPassword = hasProperty('ApiPassword') ? ApiPassword : 'APIPASSWORD'
def ServerURL = hasProperty('ServerURL') ? ServerURL : 'SERVERURL'
def AppType = hasProperty('AppType') ? AppType : 'APPTYPE'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion "30.0.0"

    defaultConfig {
        applicationId "com.matrimonymandaps.vendor"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "ApiUserName", "\"$ApiUserName\""
        buildConfigField "String", "ApiPassword", "\"$ApiPassword\""
        buildConfigField "String", "ServerURL", "\"$ServerURL\""
        buildConfigField "String", "appType", "\"$AppType\""
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }


    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }
    androidExtensions {
        features = ["parcelize"]
    }

    kapt {
        generateStubs = true
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // Kotlin specific dependencies
    implementation kotlinDependencies.kotlinStdlib
    implementation kotlinDependencies.coroutinesCore
    implementation kotlinDependencies.coroutinesAndroid

    // Core Android dependencies
    implementation androidxDependencies.core
    implementation androidxDependencies.appCompat
    implementation androidxDependencies.constraintLayout
    implementation androidxDependencies.navigationFragment
    implementation androidxDependencies.recyclerView
    implementation androidxDependencies.legacy
    implementation androidxDependencies.lifecycleExtensions

    // UI
    implementation utilLibs.circleImageView

    // Networking
    implementation networkingDependencies.retrofitCore
    implementation networkingDependencies.retrofitMoshiConverter
    implementation networkingDependencies.moshi
    kapt networkingDependencies.moshiAnnotation
    implementation networkingDependencies.okhttp
    implementation networkingDependencies.okhttpLoggingInterceptor
    implementation (networkingDependencies.picasso){
        exclude group: "com.android.support"
    }

    // Android KTX
    implementation androidKtx.activityKtx
    implementation androidKtx.fragmentKtx
    implementation androidKtx.runtimeKtx
    implementation androidKtx.navigationKtx
    implementation androidKtx.liveDataKtx

    // Test
    testImplementation testing.junit
    androidTestImplementation testing.junitAndroid
    androidTestImplementation testing.espresso
    testImplementation testing.mockk
    testImplementation testing.coroutinesTest
    testImplementation testing.androidCoreTest

    coreLibraryDesugaring utilLibs.coreDesugaring

    // Since these are alpha dependencies and subject to change they will remain outside of the
    // dependencies.gradle script till they reach 1.0.0
    implementation "com.google.dagger:hilt-android:2.28.1-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.28.1-alpha"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
    implementation 'com.karumi:dexter:6.2.1'

    implementation "com.google.firebase:firebase-core:$firebaseCore"
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$firebaseCrash"

    implementation "com.google.android.gms:play-services-gcm:17.0.0"
    implementation "com.google.android.gms:play-services-auth:18.1.0"
    implementation 'com.google.android.gms:play-services-auth-api-phone:17.4.0'



}
apply plugin: 'com.google.gms.google-services'
